# SPDX-FileCopyrightText: 2025 Robin Vobruba <hoijui.quaero@gmail.com>
#
# SPDX-License-Identifier: Unlicense

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ods: <http://w3id.org/oseg/ont/ods#>
PREFIX okh: <http://w3id.org/oseg/ont/okh#>
PREFIX okhkrawl: <http://w3id.org/oseg/ont/okhkrawl#>
PREFIX otrl: <http://w3id.org/oseg/ont/otrl#>
PREFIX schema: <https://schema.org/>
PREFIX spdx: <http://spdx.org/rdf/terms#>
PREFIX text: <http://jena.apache.org/text#>
PREFIX tsdc: <https://w3id.org/oseg/ont/tsdc/core#>
PREFIX void: <http://rdfs.org/ns/void#>

# All projects, including most of their properties.
# Something along these lines would be used when viewing the detail-view
# of a project on a web-UI, for example.
#
# If you enable the filter in the end,
# this fetches only projects that have "loom" in their name.
# Returns the first 10 finds only.
SELECT DISTINCT
  ?proj_name
  ?src_license
  ?src_license_name
  ?src_license_osi
  ( GROUP_CONCAT( DISTINCT STR(?src_licensor); separator=',' ) as ?src_licensor_list )
  ?src_url
  ?proj_license
  ?proj_license_name
  ?proj_license_osi
  ( GROUP_CONCAT( DISTINCT STR(?proj_licensor); separator=',' ) as ?proj_licensor_list )
  ?proj_function
  ( GROUP_CONCAT( DISTINCT STR(?proj_documentation_language); separator=',' ) as ?proj_documentation_language_list )
  ?proj_version
  ( GROUP_CONCAT( DISTINCT STR(?proj_organization); separator=',' ) as ?proj_organization_list )
  ( GROUP_CONCAT( DISTINCT STR(?proj_readme); separator=',' ) as ?proj_readme_list )
  ?proj_technology_readiness_level
  ?proj_technology_readiness_level_label
  ?proj_technology_readiness_level_comment
  ?proj_technology_readiness_level_goal
  ?proj_documentation_readiness_level
  ?proj_documentation_readiness_level_label
  ?proj_documentation_readiness_level_comment
  ?proj_documentation_readiness_level_goal
  ( GROUP_CONCAT( DISTINCT STR(?proj_attestation); separator=',' ) as ?proj_attestation_list )
  ?proj_doi
  ( GROUP_CONCAT( DISTINCT STR(?proj_std); separator=',' ) as ?proj_std_list )
  ( GROUP_CONCAT( DISTINCT STR(?proj_cpc); separator=',' ) as ?proj_cpc_list )
  ( GROUP_CONCAT( DISTINCT STR(?proj_tsdc_id); separator=',' ) as ?proj_tsdc_id_list )
  ( GROUP_CONCAT( DISTINCT STR(?proj_bom_url); separator=',' ) as ?proj_bom_url_list )
  ( GROUP_CONCAT( DISTINCT STR(?proj_manufacturing_instructions); separator=',' ) as ?proj_manufacturing_instructions_list )
  ( GROUP_CONCAT( DISTINCT STR(?proj_user_manual); separator=',' ) as ?proj_user_manual_list )
  ?proj_mass
  ?proj_outer_dimensions_width
  ?proj_outer_dimensions_height
  ?proj_outer_dimensions_depth
  ( GROUP_CONCAT( DISTINCT STR(?proj_img_url); separator=',' ) as ?proj_img_url_list )
#  ?proj_img_slot
#  ?proj_img_tag
#  ?proj_img_caption
  ?proj_release_url
  ?proj_normalized_manifest
  ?proj_source_url
WHERE {
  ?ds
    a ods:Dataset ;
    ods:hasSource ?src ;
    void:rootResource ?proj ;
    .
  ?src
    ods:license ?src_license ;
    ods:licensor ?src_licensor ;
    ods:dataSourcingProcedure ?src_ds_sourcing_procedure ;
    .

  { ?src ods:primaryHost okhkrawl:dataProviderAppropedia }
  UNION
  { ?src ods:primaryHost okhkrawl:dataProviderCodeberg }
  UNION
  { ?src ods:primaryHost okhkrawl:dataProviderGithub }
  UNION
  { ?src ods:primaryHost okhkrawl:dataProviderGitlab }
  UNION
  { ?src ods:primaryHost okhkrawl:dataProviderGitlabOpenSourceEcologyGermany }
  UNION
  { ?src ods:primaryHost okhkrawl:dataProviderOshwa }
# NOTE We comment this out to excldue Thingiverse
#  UNION
#  { ?src ods:primaryHost okhkrawl:dataProviderThingiverse }

  OPTIONAL {
    ?src_license
      spdx:name ?src_license_name ;
      spdx:isOsiApproved ?src_license_osi ;
      .
  }
  OPTIONAL { ?src ods:source ?src_url . }
  ?proj
    okh:name ?proj_name ;
    ods:license ?proj_license ;
    ods:licensor ?proj_licensor ;
    okh:function ?proj_function ;
  .
  OPTIONAL {
    ?proj_license
      spdx:name ?proj_license_name ;
      spdx:isOsiApproved ?proj_license_osi ;
      .
  }
  OPTIONAL { ?proj okh:documentationLanguage ?proj_documentation_language . }
  OPTIONAL { ?proj okh:version ?proj_version . }
  OPTIONAL { ?proj okh:organization ?proj_organization . }
  OPTIONAL { ?proj okh:readme ?proj_readme . }
  OPTIONAL {
    ?proj okh:technologyReadinessLevel ?proj_technology_readiness_level .
    ?proj_technology_readiness_level
      rdfs:label ?proj_technology_readiness_level_label ;
      rdfs:comment ?proj_technology_readiness_level_comment ;
      otrl:goal ?proj_technology_readiness_level_goal ;
      .
  }
  OPTIONAL {
    ?proj okh:documentationReadinessLevel ?proj_documentation_readiness_level .
    ?proj_documentation_readiness_level
      rdfs:label ?proj_documentation_readiness_level_label ;
      rdfs:comment ?proj_documentation_readiness_level_comment ;
      otrl:goal ?proj_documentation_readiness_level_goal ;
      .
  }
  OPTIONAL { ?proj okh:attestation ?proj_attestation . }
  OPTIONAL { ?proj okh:hasPublication [ okh:doi ?proj_doi ] . }
  OPTIONAL { ?proj okh:compliesWith [ okh:standardID ?proj_std ] . }
  OPTIONAL { ?proj okh:cpcPatentClass ?proj_cpc . }
  OPTIONAL { ?proj okh:tsdc [ tsdc:id ?proj_tsdc_id ] . }
  OPTIONAL { ?proj okh:hasBoM [ ods:url ?proj_bom_url ] . }
  OPTIONAL { ?proj okh:hasManufacturingInstructions [ ods:url ?proj_manufacturing_instructions ] . }
  OPTIONAL { ?proj okh:hasUserManual [ ods:url ?proj_user_manual ] . }
  OPTIONAL { ?proj okh:hasMass ?proj_mass . }
  OPTIONAL { ?proj okh:hasOuterDimensions [
      okh:width ?proj_outer_dimensions_width ;
      okh:height ?proj_outer_dimensions_height ;
      okh:depth ?proj_outer_dimensions_depth ;
    ] . }
  OPTIONAL { ?proj okh:hasImage [
      ods:url ?proj_img_url ;
#      OPTIONAL { okh:fillsSlot [ schema:termCode ?proj_img_slot ] } ;
#      OPTIONAL { okh:hasTag [ schema:termCode ?proj_img_tag ] } ;
#      OPTIONAL { okh:depicts ?proj_img_caption } ;
    ] . }
  OPTIONAL { ?proj okh:release ?proj_release_url . }
  OPTIONAL { ?proj okh:normalizedManifestContent ?proj_normalized_manifest . }
  OPTIONAL { ?proj ods:source ?proj_source_url . }

#  ?proj text:query (okh:name "loom") .
#  FILTER regex(?name, "loom", "i")
#  FILTER CONTAINS(?name, "loom")
}
GROUP BY
  ?proj_name
  ?src_license
  ?src_license_name
  ?src_license_osi
  ?src_url
  ?proj_license
  ?proj_license_name
  ?proj_license_osi
  ?proj_function
  ?proj_version
  ?proj_technology_readiness_level
  ?proj_technology_readiness_level_label
  ?proj_technology_readiness_level_comment
  ?proj_technology_readiness_level_goal
  ?proj_documentation_readiness_level
  ?proj_documentation_readiness_level_label
  ?proj_documentation_readiness_level_comment
  ?proj_documentation_readiness_level_goal
  ?proj_doi
  ?proj_mass
  ?proj_outer_dimensions_width
  ?proj_outer_dimensions_height
  ?proj_outer_dimensions_depth
#  ?proj_img_slot
#  ?proj_img_tag
#  ?proj_img_caption
  ?proj_release_url
  ?proj_normalized_manifest
  ?proj_source_url
ORDER BY ?ds
LIMIT 10
